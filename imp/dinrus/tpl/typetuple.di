
module tpl.typetuple;

/**
 * Создаёт кортеж типов в виде цепочки из нуля и более типов.
 * Пример:
 * ---
 * import tpl.typetuple;
 * alias КортежТипа!(цел, дво) TL;
 *
 * цел foo(TL td)  // то же что и цел foo(цел, дво);
 * {
 *    return td[0] + cast(цел)td[1];
 * }
 * ---
 *
 * Пример:
 * ---
 * КортежТипа!(TL, сим)
 * // это равнозначно с:
 * КортежТипа!(цел, дво, сим)
 * ---
 */
template КортежТипа(ТСписок...)
{
    alias ТСписок КортежТипа;
}

/**
 * Возвращает индекс первого случая типа T, найденного в
 * цепи из ноль или более типов в ТСписке.
 * Если не найден, будет получен ответ -1.
 * Пример:
 * ---
 * import tpl.typetuple;

 *
 * проц foo()
 * {
 *    пишифнс("Индексом дол является ",
 *          Индекс_у!(дол, КортежТипа!(цел, дол, дво)));
 *    // выводит: Индексом дол является 1
 * }
 * ---
 */
template Индекс_у(T, ТСписок...)
{
    static if (ТСписок.length == 0)
	const цел Индекс_у = -1;
    else static if (is(T == ТСписок[0]))
	const цел Индекс_у = 0;
    else
	const цел Индекс_у =
		(Индекс_у!(T, ТСписок[1 .. length]) == -1)
			? -1
			: 1 + Индекс_у!(T, ТСписок[1 .. length]);
}

/**
 * Возвращает кортеж типов, созданный из ТСписок, с первым случаем,
 * если таковой имеется, удаления T.
 * Пример:
 * ---
 * Вырезать!(дол, цел, дол, дво, сим)
 * // то же самое, что и:
 * КортежТипа!(цел, дво, сим)
 * ---
 */
template Вырезать(T, ТСписок...)
{
    static if (ТСписок.length == 0)
	alias ТСписок Вырезать;
    else static if (is(T == ТСписок[0]))
	alias ТСписок[1 .. length] Вырезать;
    else
	alias КортежТипа!(ТСписок[0], Вырезать!(T, ТСписок[1 .. length])) Вырезать;
}

/**
 * Возвращает кортеж типов, созданный из ТСписок, со всеми случаями,
 * если таковые имеются, удаления T.
 * Пример:
 * ---
 * alias КортежТипа!(цел, дол, дол, цел) TL;
 *
 * ВырезатьВсе!(дол, TL)
 * // то же самое, что и:
 * КортежТипа!(цел, цел)
 * ---
 */
template ВырезатьВсе(T, ТСписок...)
{
    static if (ТСписок.length == 0)
	alias ТСписок ВырезатьВсе;
    else static if (is(T == ТСписок[0]))
	alias ВырезатьВсе!(T, ТСписок[1 .. length]) ВырезатьВсе;
    else
	alias КортежТипа!(ТСписок[0], ВырезатьВсе!(T, ТСписок[1 .. length])) ВырезатьВсе;
}

/**
 * Возвращает кортеж типов, созданный из ТСписок, с удалением всех
 * типов-дубликатов.
 * Пример:
 * ---
 * alias КортежТипа!(цел, дол, дол, цел, плав) TL;
 *
 * БезДубликатов!(TL)
 * // то же самое, что и:
 * КортежТипа!(цел, дол, плав)
 * ---
 */
template БезДубликатов(ТСписок...)
{
    static if (ТСписок.length == 0)
	alias ТСписок БезДубликатов;
    else
	alias КортежТипа!(ТСписок[0], БезДубликатов!(ВырезатьВсе!(ТСписок[0], ТСписок[1 .. length]))) БезДубликатов;
}

/**
 * Возвращает кортеж типов, созданный из ТСписок, с первым случаем
 * типа T, если таковой обнаружен, заменённым на тип U.
 * Пример:
 * ---
 * alias КортежТипа!(цел, дол, дол, цел, плав) TL;
 *
 * Заменить!(дол, сим, TL)
 * // то же самое, что и:
 * КортежТипа!(цел, сим, дол, цел, плав)
 * ---
 */
template Заменить(T, U, ТСписок...)
{
    static if (ТСписок.length == 0)
	alias ТСписок Заменить;
    else static if (is(T == ТСписок[0]))
	alias КортежТипа!(U, ТСписок[1 .. length]) Заменить;
    else
	alias КортежТипа!(ТСписок[0], Заменить!(T, U, ТСписок[1 .. length])) Заменить;
}

/**
 * Возвращает кортеж типов, созданный из ТСписок, со всеми случаями
 * типа T, если таковые обнаружены, заменёнными на тип U.
 * Пример:
 * ---
 * alias КортежТипа!(цел, дол, дол, цел, плав) TL;
 *
 * ЗаменитьВсе!(дол, сим, TL)
 * // то же самое, что и:
 * КортежТипа!(цел, сим, сим, цел, плав)
 * ---
 */
template ЗаменитьВсе(T, U, ТСписок...)
{
    static if (ТСписок.length == 0)
	alias ТСписок ЗаменитьВсе;
    else static if (is(T == ТСписок[0]))
	alias КортежТипа!(U, ЗаменитьВсе!(T, U, ТСписок[1 .. length])) ЗаменитьВсе;
    else
	alias КортежТипа!(ТСписок[0], ЗаменитьВсе!(T, U, ТСписок[1 .. length])) ЗаменитьВсе;
}

/**
 * Возвращает кортеж типов, созданный из ТСписок, в реверсированном порядке.
 * Пример:
 * ---
 * alias КортежТипа!(цел, дол, дол, цел, плав) TL;
 *
 * Реверсировать!(TL)
 * // то же самое, что и:
 * КортежТипа!(плав, цел, дол, дол, цел)
 * ---
 */
template Реверсировать(ТСписок...)
{
    static if (ТСписок.length == 0)
	alias ТСписок Реверсировать;
    else
	alias КортежТипа!(Реверсировать!(ТСписок[1 .. length]), ТСписок[0]) Реверсировать;
}

/**
 * Возвращает тип из ТСписок, которыйнаиболее производен от типа T.
 * Если ничегоне найдено, возвращается T.
 * Пример:
 * ---
 * class A { }
 * class B : A { }
 * class C : B { }
 * alias КортежТипа!(A, C, B) TL;
 *
 * ПоследнийПроизводный!(Object, TL) x;  // x is declared as тип C
 * ---
 */
template ПоследнийПроизводный(T, ТСписок...)
{
    static if (ТСписок.length == 0)
	alias T ПоследнийПроизводный;
    else static if (is(ТСписок[0] : T))
	alias ПоследнийПроизводный!(ТСписок[0], ТСписок[1 .. length]) ПоследнийПроизводный;
    else
	alias ПоследнийПроизводный!(T, ТСписок[1 .. length]) ПоследнийПроизводный;
}

/**
 * Возвращает кортеж типов ТСписок с типами, отсортированными так, что
 * наиболее производные типы следуют первыми.
 * Пример:
 * ---
 * class A { }
 * class B : A { }
 * class C : B { }
 * alias КортежТипа!(A, C, B) TL;
 *
 * ПроизводныйВперёд!(TL)
 * // то же самое, что и:
 * КортежТипа!(C, B, A)
 * ---
 */
template ПроизводныйВперёд(ТСписок...)
{
    static if (ТСписок.length == 0)
	alias ТСписок ПроизводныйВперёд;
    else
	alias КортежТипа!(ПоследнийПроизводный!(ТСписок[0], ТСписок[1 .. length]),
	                ПроизводныйВперёд!(ЗаменитьВсе!(ПоследнийПроизводный!(ТСписок[0], ТСписок[1 .. length]),
						    ТСписок[0],
						    ТСписок[1 .. length]))) ПроизводныйВперёд;
}
